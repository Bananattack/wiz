package sgb do
    func send_packet do
        // Arguments: hl = pointer to 16-byte packet.
        // Dirty: all registers.
        c = 0
        [gb.joy.ctrl:c] = 0 via a
        [gb.joy.ctrl:c] = 0x30 via a

        b = 16
        loop
            e = 8
            d = [hl++] via a
            loop
                a = 0x10
                if d = d >> 1 is not carry then
                    a = 0x20
                end
                [gb.joy.ctrl:c] = a
                [gb.joy.ctrl:c] = 0x30 via a
                e--
            until zero end
            b--
        until zero end
        [gb.joy.ctrl:c] = 0x20 via a
        [gb.joy.ctrl:c] = 0x30 via a

        c = 4
        loop
            call video.delay_frame
            c--
        until zero end
    end

    func detect do
        // Dirty: all registers.
        // Returns: b = 1 when SGB, b = 0 when not.
        hl = ram.t0
        [hl++] = gb.super.MLT_REQ | 1 via a
        [hl++] = 0x01 via a
        a = 0
        bc = 14
        call memset
        hl = ram.t0
        call sgb.send_packet
        call video.delay_frame

        b = 0
        a = [gb.joy.ctrl] & 3
        if compare a to 3 is == then
            // Read up/down/left/right.
            [gb.joy.ctrl] = gb.joy.select.DIRECTIONS via a
            // Read twice are to prevent bounce.
            unroll 2 do 
                a = [gb.joy.ctrl]
            end
            // Reset the joypad port.
            [gb.joy.ctrl] = gb.joy.select.DIRECTIONS | gb.joy.select.BUTTONS via a
            // Read A/B/Select/Start
            [gb.joy.ctrl] = gb.joy.select.BUTTONS via a
            // Read 6 times to prevent bounce.
            unroll 6 do
                a = [gb.joy.ctrl]
            end
            // Reset the joypad port. (Get the joypad index)
            [gb.joy.ctrl] = gb.joy.select.DIRECTIONS | gb.joy.select.BUTTONS via a
            // Read 6 times to prevent bounce.
            unroll 4 do
                a = [gb.joy.ctrl]
            end
            a = [gb.joy.ctrl] & 3
            compare a to 3
            return when ==
        end

        hl = ram.t0 + 1
        [hl++] = 0x00 via a
        hl = ram.t0
        call sgb.send_packet
        b = 1
    end

    func prepare_transfer do
        // Preconditions: Video is disabled.
        [gb.scroll.x] = 0 via a
        [gb.scroll.y] = a
        [gb.mono.palette.bg] = 0b11100100 via a

        hl = gb.ram.map
        de = 12
        a = 0
        b = 18
        loop
            c = 20
            loop
                [hl++] = a
                a++
                c--
            until zero end
            hl = hl + de
            b--
        until zero end

        [gb.lcd.ctrl] = gb.lcd.CTRL_ON | gb.lcd.CTRL_TILESET2 | gb.lcd.CTRL_BG_ON via a
    end
end