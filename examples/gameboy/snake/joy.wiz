package joy do
    func init do
        [unpress] = 0 via a
        [pressed] = 0 via a
    end

    func check do
        // Read up/down/left/right.
        [gb.joy.ctrl] = gb.joy.select.DIRECTIONS via a
        // Read twice are to prevent bounce.
        inline for let i = 1, 2 do 
            a = [gb.joy.ctrl]
        end
        // a = 0000dulr
        a = ~a & 0xF
        // a = dulr0000
        a = <>a
        // controls = a
        c = a

        // Read A/B/Select/Start
        [gb.joy.ctrl] = gb.joy.select.BUTTONS via a
        // Read 6 times to prevent bounce.
        inline for let i = 1, 6 do
            a = [gb.joy.ctrl]
        end
        // a = dulrssba
        a = ~a & 0xF | c
        // controls = a
        c = a
        
        // Remove unpress flag for controls no longer being held.
        // unpress = unpress & controls
        [unpress] = [unpress] & c via a
        // Only keep controls that don't have the unpress flag set.
        [pressed] = ~a & c via a
        // Reset the joypad port.
        [gb.joy.ctrl] = gb.joy.select.DIRECTIONS | gb.joy.select.BUTTONS via a
    end
end